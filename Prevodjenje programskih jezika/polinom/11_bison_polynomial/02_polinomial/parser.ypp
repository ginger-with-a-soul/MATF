%{
#include <iostream>
#include <string>
#include "polinomial.hpp"

extern int yylex();

void yyerror(std::string s)
{
    std::cerr << "Sintaksna greska: " << s << std::endl;
    exit(EXIT_FAILURE);
}
%}

%union {
    polinomial* poly_type;
    float float_type;
    std::string* str_type;
}

%type <poly_type> E Niz_koef
%token <float_type> num_token
%token <str_type> id_token

%left '+' '-'
%left '*'
%left UMINUS

%%
Program
    : Program Naredba
    | Naredba
    ;

Naredba
    : E '\n' {
        std::cout << *$1 << std::endl;
    }
    ;

E
    : E '+' E {
        // -------------------------------------------------------------------------
        // | BITAN DETALJ
        // -------------------------------------------------------------------------
        // | $$ = *$1 + *$3 nije dobro jer je rezultat izracunavanja
        // | *$1 + *$3 lokalna promenljiva na steku dok je tip od $$
        // | pokaziva na polinom. Odnosno, neophodno je da dinamicki alociramo
        // | polinom. Koristimo podrazumevani konstruktor kopije koji poziva
        // | konstruktore kopije za podatke unutar klase, sto je u nasem slucaju
        // | konstruktor kopije za vektor.
        // -------------------------------------------------------------------------
        $$ = new polinomial(*$1 + *$3);
    } | E '-' E {
        $$ = new polinomial(*$1 - *$3);
    } | E '*' E {
        $$ = new polinomial(*$1 * *$3);
    } | '<' Niz_koef '>' {
        $$ = $2;
    }
    | '-' E %prec UMINUS {
        $$ = new polinomial($2->uminus());
    }
    ;

Niz_koef
    : Niz_koef ',' num_token {
        $1->add_coef($3);
        $$ = $1;
    }
    | num_token {
        // $$ -> Niz_koef -> polinom*
        // $1 -> num_token -> float
        $$ = new polinomial();
        $$->add_coef($1);
    }
    ;
%%
int main()
{
    yyparse();
    return 0;
}
